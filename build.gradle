buildscript {
	repositories {
		maven { url = 'https://maven.minecraftforge.net' }
		mavenCentral()
		maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
		classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
	}
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

// Maven info
archivesBaseName = project.archives_base_name
version = "${minecraft_version}-${mod_version}"
group = project.maven_group

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
	mappings channel: 'official', version: '1.18.1'

	runs {
		client {
			workingDirectory project.file('run')
//            property 'forge.logging.markers', 'REGISTRIES'
//            property 'forge.logging.console.level', 'debug'

			mods {
				faketnt {
					source sourceSets.main
				}
			}
		}

		server {
			workingDirectory project.file('run')
//            property 'forge.logging.markers', 'REGISTRIES'
//            property 'forge.logging.console.level', 'debug'

			mods {
				faketnt {
					source sourceSets.main
				}
			}
		}

		data {
			workingDirectory project.file('run')
//            property 'forge.logging.markers', 'REGISTRIES'
//            property 'forge.logging.console.level', 'debug'

			// Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
			args '--mod', "faketnt", '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

			mods {
				faketnt {
					source sourceSets.main
				}
			}
		}
	}
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
	minecraft 'net.minecraftforge:forge:1.18.1-39.0.7'
	implementation fg.deobf("com.yungnickyoung.minecraft.yungsapi:YungsApi:${yungs_api_version}")
	if (System.getProperty("idea.sync.active") != "true") {
		annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
	}
}

jar {
	manifest {
		attributes([
				"Specification-Title"     : mod_id,
				"Specification-Vendor"    : "YUNGNICKYOUNG",
				"Specification-Version"   : "1", // We are version 1 of ourselves
				"Implementation-Title"    : project.name,
				"Implementation-Version"  : "${version}",
				"Implementation-Vendor"   : "YUNGNICKYOUNG",
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
				"Signing-Fingerprint"     : project.hasProperty('signSHA1') ? project.findProperty('signSHA1') : "unsigned",
				"MixinConfigs": "faketnt.mixins.json"
		])
	}
}

jar.finalizedBy('reobfJar')

mixin {
	add sourceSets.main, "faketnt.refmap.json"
}